plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management'
}

dependencies {
    implementation project(':insurance-connector-domain')
    implementation project(':insurance-connector-infrastructure')
    implementation 'org.springframework.boot:spring-boot-starter'
}

springBoot {
    mainClass = 'com.lulobank.insurance.InsuranceConnectorApplication'
    buildInfo()
}

jar {
    enabled = false
}

bootJar {
    dependsOn('copyJmxPrometheus')
    archiveClassifier.set("")
}

tasks.register('copyJmxPrometheus', Copy) {
    from project(':insurance-connector-infrastructure').configurations.runtimeClasspath
    into layout.buildDirectory.dir("resources/main/agents")
    include "**/jmx-prometheus-${jmxPrometheus}.jar"
    rename { String fileName -> fileName.replaceAll(/jmx-prometheus-\d+\.\d+\.\d?\.jar/, 'jmx-prometheus.jar')
    }
    outputs.dir layout.buildDirectory.dir("resources/main/agents")

    doLast {
        println "jmx-prometheus.jar copiado a: ${layout.buildDirectory.dir("resources/main/agents").get().asFile}"
    }

}

tasks.named('processResources') {
    mustRunAfter copyJmxPrometheus
}

tasks.named('classes') {
    mustRunAfter copyJmxPrometheus
}

tasks.named('resolveMainClassName') {
    mustRunAfter copyJmxPrometheus
}

tasks.named('bootJar') {
    mustRunAfter copyJmxPrometheus
}